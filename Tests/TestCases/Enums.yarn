title: Start
---

// Create a new enum, "Food". Variables of this type can store one of these
// three cases:
<<enum Food>>
    <<case Apple>>
    <<case Orange>>
    <<case Pear>>
<<endenum>>

// Declare a new variable, of this type.
<<declare $favouriteFood = Food.Apple as Food>>

// The 'as Food' can be left out, because the compiler will figure it out
<<declare $secondFavouriteFood = Food.Orange>>

// Enum variables can be compared to each other
<<if $favouriteFood == Food.Apple>>
    I like apples!
<<elseif $favouriteFood == Food.Orange>>
    It's an error if you see this!
<<else>>
    It's an error if you see this!
<<endif>>

// Enum variables can be modified, just like all other variables
<<set $favouriteFood to Food.Orange>>

// (Just checking that we're not just evaluating all comparisons as true
// lol)
<<if $favouriteFood == Food.Apple>>
    It's an error if you see this!
<<elseif $favouriteFood != Food.Orange>>
    It's an error if you see this!
<<else>>
    I like oranges now!
<<endif>>

// Declare two new enums that have explicit raw values: one of numbers, and
// one of strings

<<enum Planets>>
    <<case Mercury = 1>>
    <<case Venus>>
    <<case Earth>>
    <<case Mars>>
    <<case Jupiter>>
    <<case Saturn>>
    <<case Uranus>>
    <<case Neptune>>
<<endenum>>

<<enum ImportantNumbers>>
    <<case HackerNumber = 1337>>
    <<case HackerNumberPlusOne>>
    <<case UltimateAnswer = 42>>
    <<case UltimateAnswerPlusOne>>
<<endenum>>

<<enum QuestObjectives>>
    <<case Objective1 = "DoObjective1">>
    <<case Objective2 = "DoObjective2">>
    <<case Objective3 = "DoObjective3">>
<<endenum>>

<<if string(QuestObjectives.Objective1) == "DoObjective1">>
    ok 1
<<endif>>

<<if number(Planets.Earth) == 3>>
    ok 2
<<endif>>

<<if number(ImportantNumbers.HackerNumber) == 1337>>
    ok 3
<<endif>>

<<if number(ImportantNumbers.HackerNumberPlusOne) == 1338>>
    ok 4
<<endif>>

<<if number(ImportantNumbers.UltimateAnswer) == 42>>
    ok 5
<<endif>>

<<if number(ImportantNumbers.UltimateAnswerPlusOne) == 43>>
    ok 6
<<endif>>


===
